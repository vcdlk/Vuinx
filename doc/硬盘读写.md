- 扇区大小：硬盘读写的最小单位，读取最小1个扇区，最多256个扇区
- 磁盘从外测技术，所以一般情况下C盘读写最快

## 读写硬盘 IDE / ATA PIO Mode

Port Input Output 端口输出输入模式

端口是外部设备内部的寄存器：

- IDE Intergrated Drive Electronics 集成电子驱动器
- ATA   Advanced Technology Attachment
American National Standards Institute ANSI  把IDE标准化成为ATA
- SATA / PATA

## 硬盘读写

- CHS模式 / Cylinder Head Sector [根据柱面，磁头，扇区读取]
- LBA 模式 Logical Block Address

LBA28 能访问128G的磁盘空间

硬盘控制端口

| Primary 通道            | Secondary 通道 | in 操作      | out 操作     |
| ----------------------- | -------------- | ------------ | ------------ |
| 0x1F0                   | 0x170          | Data         | Data         |
| 0x1F1                   | 0x171          | Error        | Features     |
| 0x1F2                   | 0x172          | Sector count | Sector count |
| 0x1F3                   | 0x173          | LBA low      | LBA low      |
| 0x1F4                   | 0x174          | LBA mid      | LBA mid      |
| 0x1F5                   | 0x175          | LBA high     | LBA high     |
| 0x1F6                   | 0x176          | Device       | Device       |
| 0x1F7                   | 0x177          | Status       | Command      |

- 0x1F0：16bit 端口，用于读写数据
- 0x1F1：检测前一个指令的错误
- 0x1F2：读写扇区的数量
- 0x1F3：起始扇区的 0 ~ 7 位
- 0x1F4：起始扇区的 8 ~ 15 位
- 0x1F5：起始扇区的 16 ~ 23 位
- 0x1F6:
    - 0 ~ 3：起始扇区的 24 ~ 27 位
    - 4: 0 主盘, 1 从片
    - 6: 0 CHS, 1 LBA
    - 5 ~ 7：固定为1
- 0x1F7: out
    - 0xEC: 识别硬盘
    - 0x20: 读硬盘
    - 0x30: 写硬盘
- 0x1F7: in / 8bit
    - 0 ERR
    - 3 DRQ 数据准备完毕
    - 7 BSY 硬盘繁忙


```c
write_disk:
    ; 向0x1f2端口写bl 也就是1扇区
    mov dx, 0x1f2
    mov al, bl
    out dx, al
    ;向0x1f3写 前8位
    inc dx;  0x1f3
    mov al, cl
    out dx, al
    ;写0x1f4,起始扇区中8位
    inc dx
    shr ecx, 8
    mov al, cl
    out dx, al
    ;写0x1f5,起始扇区高8位
    inc dx
    shr ecx, 8
    mov al, cl
    out dx, al
    
    inc dx ;写0x1f6
    shr ecx,8
    and cl,0b1111;0x1f4高四位为0

    mov al, 0b1110_0000
    or al,cl
    out dx, al

    inc dx;0x1f7
    mov al, 0x30
    out dx, al

    xor ecx, ecx ;ecx清空
    mov cl, bl

    .write: 
        push cx
        call .writes    ; 写一个扇区
        call .waits     ; 等待硬盘繁忙
        pop cx
        loop .write
    ret

    .waits:
        mov dx, 0x1f7
        .check:
            in al, dx
            jmp $+2; nop 直接跳转到下一行
            jmp $+2; 一点点延迟
            jmp $+2
            and al, 0b1000_0000
            cmp al, 0b0000_0000
            jnz .check
        ret
    
    .writes:
        mov dx, 0x1f0
        mov cx, 256
        .writew:
            mov ax, [edi]
            out dx, ax
            jmp $ + 2
            jmp $ + 2
            jmp $ + 2
            add edi, 2
            loop .writew
        ret
```

## 参考文献

- <https://www.youtube.com/watch?v=oEORcCQ62nQ>
- <https://wiki.osdev.org/ATA_PIO_Mode>
- <https://bochs.sourceforge.io/techspec/PORTS.LST>
- <https://www.techtarget.com/searchstorage/definition/IDE>
